set(EXT_SQL_FILE ${PROJECT_NAME}--${PROJECT_VERSION_MOD}.sql)
set(EXT_SQL_UPDATE_FILE ${PROJECT_NAME}--${UPDATE_FROM_VERSION}--${PROJECT_VERSION_MOD}.sql)
set(EXT_SQL_UPDATE_PRE_FILE updates/pre-${UPDATE_FROM_VERSION}--${PROJECT_VERSION_MOD}.sql)
set(EXT_SQL_UPDATE_POST_FILE updates/post-${UPDATE_FROM_VERSION}--${PROJECT_VERSION_MOD}.sql)
set(EXT_SQL_UPDATE_PRE_GLOBAL_FILE updates/pre-global.sql)
set(EXT_SQL_UPDATE_POST_GLOBAL_FILE updates/post-global.sql)
file(STRINGS extra_extension_files.txt EXT_SQL_EXTRA_FILES)
file(STRINGS load_order.txt SQL_FILES)
set(UPDATE_FILE_LIST ${EXT_SQL_UPDATE_PRE_GLOBAL_FILE} ${EXT_SQL_UPDATE_PRE_FILE} ${SQL_FILES} ${EXT_SQL_UPDATE_POST_FILE}  ${EXT_SQL_UPDATE_POST_GLOBAL_FILE})

if (WIN32)
  # Make list of files into string of files separated by "+"
  # to make Windows copy concatenate them
  string(REPLACE ";" ";+" SQL_FILES_JOINED "${SQL_FILES}")
  string(REPLACE ";" ";+" UPDATE_FILES_JOINED "${UPDATE_FILE_LIST}")
  # Windows copy command requires backslashes for relative paths
  string(REPLACE "/" "\\" EXT_SQL_UPDATE_PRE_FILE "${EXT_SQL_UPDATE_PRE_FILE}")
  string(REPLACE "/" "\\" EXT_SQL_UPDATE_POST_FILE "${EXT_SQL_UPDATE_POST_FILE}")

  set(CAT_SQL_FILE_CMD copy /y ${SQL_FILES_JOINED} "\"${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_FILE}\"" >NUL)
  set(CAT_SQL_UPDATE_FILE_CMD copy /y ${UPDATE_FILES_JOINED} "\"${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_UPDATE_FILE}\"" >NUL)
else ()
  set(CAT_SQL_FILE_CMD cat ${SQL_FILES} > ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_FILE})
  set(CAT_SQL_UPDATE_FILE_CMD cat ${UPDATE_FILE_LIST} > ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_UPDATE_FILE})
endif ()

# Command and target for the SQL file
add_custom_command(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_FILE}
  DEPENDS ${SQL_FILES}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  COMMAND ${CAT_SQL_FILE_CMD}
  COMMENT "Generating ${EXT_SQL_FILE}")

add_custom_target(sqlfile ALL DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_FILE})

# Command and target for the update SQL file
add_custom_command(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_UPDATE_FILE}
  DEPENDS ${SQL_FILES} ${EXT_SQL_UPDATE_PRE_FILE} ${EXT_SQL_UPDATE_POST_FILE}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  COMMAND ${CAT_SQL_UPDATE_FILE_CMD}
  COMMENT "Generating ${EXT_SQL_UPDATE_FILE}")

add_custom_target(sqlupdatefile ALL DEPENDS ${EXT_SQL_UPDATE_FILE})

install(
  FILES ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_FILE}
  ${CMAKE_CURRENT_BINARY_DIR}/${EXT_SQL_UPDATE_FILE}
  ${EXT_SQL_EXTRA_FILES}
  DESTINATION "${PG_SHAREDIR}/extension")
